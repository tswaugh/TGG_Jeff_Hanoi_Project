&ACCESS RVP
&REL 60
&PARAM TEMPLATE = C:\KRC\Roboter\Template\ExpertVorgabe
&PARAM EDITMASK = *
DEF SOLVE_HANOI()
;------- Declaration section --------
EXT BAS(BAS_COMMAND:IN, REAL:IN)
DECL INT FROM_TOWER, USING_TOWER, TO_TOWER, NEXT_INDEX, LAST_FROM, CYCLE_COUNT
DECL AXIS HOME

;---------- Initialization ----------
; Set the Max velocities and accelerations
BAS(#INITMOV, 0)   ; Init all Motion Parameters
BAS(#ACC_PTP, 100) ; PTP-Acceleration in %
BAS(#VEL_PTP, 100) ; PTP-Speed in %
BAS(#ACC_CP, 100)  ; LIN-Acceleration in %
BAS(#VEL_CP, 2)    ; LIN-Speed in m/s

; Set Axis 5 to -0.1 to prevent singularity problems
; Not the cleanest solution, but works for now
HOME={AXIS: A1 0, A2 -90, A3 90, A4 0, A5 -0.1, A6 0}

; Preset Starting Towers
FROM_TOWER = 1
USING_TOWER = 2
TO_TOWER = 3
NEXT_INDEX = 1

; Set Disk Counters
DISK_COUNTS[1] = 0
DISK_COUNTS[2] = 0
DISK_COUNTS[3] = 0
DISK_COUNTS[FROM_TOWER] = HANOI_DISK_COUNT

;----------- Main section ----------
;Do this loop forever
WHILE TRUE
PTP HOME ; BCO run - setup for movements
FirstMove = TRUE

MOVETOWER(HANOI_DISK_COUNT, FROM_TOWER, USING_TOWER, TO_TOWER, MOVE_ARRAY[,], NEXT_INDEX)

; Sanity check
IF NEXT_INDEX <> REQUIRED_MOVES + 1 THEN
  HALT
ENDIF 

;Execute Movements
$TOOL = DiskTool
EXECUTE_SOLUTION(MOVE_ARRAY[,])
$TOOL = $NULLFRAME

; Shuffle the to, from, and using towers
LAST_FROM = FROM_TOWER
FROM_TOWER = TO_TOWER
TO_TOWER = USING_TOWER
USING_TOWER = LAST_FROM
NEXT_INDEX = 1
ENDWHILE ;Infinite demo loop

PTP HOME
END ;Main program END

; ------------EXECUTE_SOLUTION()------------
; EXECUTE_SOLUTION will execute the disk movements in MOVE_MATRIX
DEF EXECUTE_SOLUTION(MOVE_MATRIX[,])

DECL INT MOVE_MATRIX[,]
DECL INT CURR_FROM, CURR_TO
DECL REAL FROM_NEXT_DISK_Z, TO_NEXT_DISK_Z

DECL POS TOWER_BASES[3]
DECL POS TABLE, TOWER_ONE_BASE_REL, TOWER_TWO_BASE_REL, TOWER_THREE_BASE_REL, TRANSIT_OFFSET, PICK_UP_OFFSET, FROM_DISK_Z_OFFSET, TO_DISK_Z_OFFSET

; Create the position vectors we'll use throughout executing the solution
TABLE = {POS: X 0, Y 0, Z 0, A 0, B 0, C 0}
TABLE.X = TABLE_X
TABLE.Y = TABLE_Y
TABLE.Z = TABLE_Z

; Tower bases are relative to the table base
TOWER_ONE_BASE_REL = {POS: X 0, Y 0, Z 0, A 0, B 0, C 0}
TOWER_ONE_BASE_REL.X = TOWER_ONE_X
TOWER_ONE_BASE_REL.Y = TOWER_ONE_Y
TOWER_ONE_BASE_REL.Z = TOWER_ONE_Z

TOWER_TWO_BASE_REL = {POS: X 0, Y 0, Z 0, A 0, B 0, C 0}
TOWER_TWO_BASE_REL.X = TOWER_TWO_X
TOWER_TWO_BASE_REL.Y = TOWER_TWO_Y
TOWER_TWO_BASE_REL.Z = TOWER_TWO_Z

TOWER_THREE_BASE_REL = {POS: X 0, Y 0, Z 0, A 0, B 0, C 0}
TOWER_THREE_BASE_REL.X = TOWER_THREE_X
TOWER_THREE_BASE_REL.Y = TOWER_THREE_Y
TOWER_THREE_BASE_REL.Z = TOWER_THREE_Z

; Pickup zone relative vector
PICK_UP_OFFSET = {POS: X 0, Y 0, Z 0, A 0, B 0, C 0}
PICK_UP_OFFSET.X = PICK_UP_X_OFFSET

; Transit zone relative vector
TRANSIT_OFFSET = {POS: X 0, Y 0, Z 0, A 0, B 0, C 0}
TRANSIT_OFFSET.Z = TRANSIT_ZONE_Z_OFFSET

; Absolute tower positions
TOWER_BASES[1] = TABLE:TOWER_ONE_BASE_REL
TOWER_BASES[2] = TABLE:TOWER_TWO_BASE_REL
TOWER_BASES[3] = TABLE:TOWER_THREE_BASE_REL

; ------MAIN EXEC LOOP------
; Execute solution
FOR I = 1 to REQUIRED_MOVES
; Initialize current to and from tower
CURR_FROM = MOVE_MATRIX[I,1]
CURR_TO = MOVE_MATRIX[I,2]

FROM_NEXT_DISK_Z = (DISK_COUNTS[CURR_FROM] - 1) * DISK_THICKNESS - DISK_PICKUP_OFFSET
TO_NEXT_DISK_Z = DISK_COUNTS[CURR_TO] * DISK_THICKNESS - DISK_PICKUP_OFFSET

FROM_DISK_Z_OFFSET = {POS: X 0, Y 0, Z 0, A 0, B 0, C 0}
FROM_DISK_Z_OFFSET.Z = FROM_NEXT_DISK_Z
TO_DISK_Z_OFFSET = {POS: X 0, Y 0, Z 0, A 0, B 0, C 0}
TO_DISK_Z_OFFSET.Z = TO_NEXT_DISK_Z

; Check if from tower has  >= 1 disks on it and that to tower has positive number of disks (SANITY CHECK)
IF DISK_COUNTS[CURR_FROM] < 1 THEN
  HALT
ENDIF

IF DISK_COUNTS[CURR_TO]  < 0 THEN
  HALT
ENDIF

; Move relative to the "from" tower
$BASE = TOWER_BASES[CURR_FROM]

; Move to the pickup zone and align with next disk to pickup
IF FirstMove THEN
  PTP PICK_UP_OFFSET
  FirstMove = FALSE
ELSE
  LIN PICK_UP_OFFSET
ENDIF

; Move to tower center offset up by currdisk offset
LIN FROM_DISK_Z_OFFSET

; Move up to transit zone
LIN TRANSIT_OFFSET

; Move relative to the "to" tower
$BASE = TOWER_BASES[CURR_TO]

; Align with to tower in transit zone
LIN TRANSIT_OFFSET

; Lower to tower base + disk offset
LIN TO_DISK_Z_OFFSET

; Retract into pickup zone
LIN PICK_UP_OFFSET

; Reset relative movement
$BASE = $NULLFRAME

; Change disk counts for from and to towers
DISK_COUNTS[CURR_TO] = DISK_COUNTS[CURR_TO] + 1
DISK_COUNTS[CURR_FROM] = DISK_COUNTS[CURR_FROM] - 1
ENDFOR

END ;Execute solution end

DEF MOVETOWER(DNUM:IN, FROM_TOWER:IN, USING_TOWER:IN, TO_TOWER:IN, MOVE_MATRIX[,]:OUT, NEXT_INDEX:OUT)
; Declare the inputs. 
DECL INT DNUM , FROM_TOWER , USING_TOWER , TO_TOWER , NEXT_INDEX
; Define MOVE_MATRIX again. It must be done.
DECL INT MOVE_MATRIX[,]

; Recursion base case
IF DNUM == 0 THEN
  RETURN
ENDIF

; Temporarily move everything on top of the FROM_TOWER except the last disk to the USING_TOWER
MOVETOWER(DNUM - 1 , FROM_TOWER , TO_TOWER , USING_TOWER , MOVE_MATRIX[,] , NEXT_INDEX)

; Move the bottom disk to the TO_TOWER
MOVE_MATRIX[NEXT_INDEX,1] = FROM_TOWER
MOVE_MATRIX[NEXT_INDEX,2] = TO_TOWER
NEXT_INDEX = NEXT_INDEX + 1

; Move the temporary stack from step 1 to the TO_TOWER
MOVETOWER(DNUM - 1 , USING_TOWER , FROM_TOWER , TO_TOWER , MOVE_MATRIX[,] , NEXT_INDEX)
END ;Movetower end